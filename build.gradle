// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.buildConfig = [
            'compileSdk': 33,
            'minSdk'    : 24,
            'targetSdk' : 33,
            'version'   : [
                    'major': 2,
                    'minor': 26,
                    'patch': 0,
                    'build': 0,
            ],
    ]

    ext.versions = [
            'kotlin'                 : '1.7.0',
            'kotlinCoroutines'       : '1.4.3-native-mt',
            'kotlinSerialization'    : '0.13.0',
            'ktor'                   : '1.2.1',
            'okhttp'                 : '4.9.0',
            'retrofit'               : '2.9.0',
            'dagger'                 : '2.42',
            'mockito'                : '3.8.0',
            'ktx'                    : '1.3.2',
            'navigation'             : '2.3.4',
            'lifecycle'              : '2.3.0',
            'extensions'             : '2.2.0',
            'mockk'                  : '1.12.2',
            'appcompat'              : '1.3.0-alpha02',
            'material'               : '1.4.0-alpha01',
            'jacoco'                 : '0.8.6',
            'junit'                  : '4.13.2',
            'glide'                  : '4.12.0',
            'androidxBrowser'        : '1.3.0',
            'constraintLayout'       : '2.0.4',
            'viewpager2'             : '1.0.0',
            'legacy'                 : '1.0.0',
            'androidxTest'           : '1.3.0',
            'androidxTestCore'       : '2.1.0',
            'androidxSavedState'     : '1.0.0',
            'gson'                   : '2.8.6',
            'flexBox'                : '3.0.0',
            'klint'                  : '0.27.0',
            'junitExt'               : '1.1.2',
            'junitEspresso'          : '3.3.0',
            "json"                   : '20220320',
            "detekt"                 : '1.18.1',
            'timber'                 : '5.0.0-SNAPSHOT',
            'trustkit'               : '1.1.5',
            'certificateTransparency': '0.2.0',
            'lottie'                 : '6.0.1',
            'youtubeExtractor'       : '0.0.6',
            'appcompatv7'            : '25.0.1',
            'playServicesAuth'       : '20.6.0',
            'easypermissions'        : '0.3.0',
            'firestore'              : '24.7.0',
    ]

    ext.deps = [
            'kotlin'                 : [
                    'stdlib'       : [
                            'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
                    ],
                    'test'         : [
                            'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common:${versions.kotlin}",
                            'jdk'        : "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}",
                    ],
                    'coroutines'   : [
                            'jdk8'   : "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${versions.kotlinCoroutines}",
                            'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoroutines}",
                    ],
                    'serialization': [
                            'jdk': "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${versions.kotlinSerialization}",
                    ]
            ],
            'trustkit'               : "com.datatheorem.android.trustkit:trustkit:${versions.trustkit}",
            'certificateTransparency': "com.babylon.certificatetransparency:certificatetransparency-android:${versions.certificateTransparency}",
            'timber'                 : [
                    'common' : "com.jakewharton.timber:timber-common:${versions.timber}",
                    'jdk'    : "com.jakewharton.timber:timber-jdk:${versions.timber}",
                    'android': "com.jakewharton.timber:timber-android:${versions.timber}",
            ],
            'androidx'               : [
                    'core'            : "androidx.core:core-ktx:${versions.ktx}",
                    'appCompat'       : "androidx.appcompat:appcompat:${versions.appcompat}",
                    'browser'         : "androidx.browser:browser:${versions.androidxBrowser}",
                    'constraintLayout': "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}",
                    'crypto'          : "androidx.security:security-crypto:${versions.crypto}",
                    'viewpager2'      : "androidx.viewpager2:viewpager2:${versions.viewpager2}",
                    'legacy'          : [
                            'v4': "androidx.legacy:legacy-support-v4:${versions.legacy}"
                    ],
                    'test'            : [
                            'runner'          : "androidx.test:runner:${versions.androidxTest}",
                            'rules'           : "androidx.test:rules:${versions.androidxTest}",
                            'testingCore'     : "androidx.arch.core:core-testing:${versions.androidxTestCore}",
                            'testingCoroutine': 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
                    ],
                    'lifecycle'       : [
                            'viewmodel' : "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle}",
                            'livedata'  : "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle}",
                            'java8'     : "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle}",
                            'extensions': "androidx.lifecycle:lifecycle-extensions:${versions.extensions}",
                            'savedstate': "androidx.lifecycle:lifecycle-viewmodel-savedstate:${versions.androidxSavedState}"
                    ],
                    'navigation'      : [
                            'fragment': "androidx.navigation:navigation-fragment-ktx:${versions.navigation}",
                            'ui'      : "androidx.navigation:navigation-ui-ktx:${versions.navigation}"
                    ],
            ],
            'material'               : "com.google.android.material:material:${versions.material}",
            'okhttp'                 : [
                    'client'       : "com.squareup.okhttp3:okhttp:${versions.okhttp}",
                    'logging'      : "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}",
                    'mockWebServer': "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
            ],
            'retrofit'               : [
                    'client'                       : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                    'converterKotlinxSerialization': 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.4.0',
                    'rx'                           : "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
                    'gson'                         : "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
            ],
            'dagger'                 : [
                    'runtime'         : "com.google.dagger:dagger:${versions.dagger}",
                    'compiler'        : "com.google.dagger:dagger-compiler:${versions.dagger}",
                    'android'         : "com.google.dagger:dagger-android:${versions.dagger}",
                    'androidSupport'  : "com.google.dagger:dagger-android-support:${versions.dagger}",
                    'androidProcessor': "com.google.dagger:dagger-android-processor:${versions.dagger}",
            ],
            'junit'                  : "junit:junit:${versions.junit}",
            'flexbox'                : "com.google.android.flexbox:flexbox:${versions.flexBox}",
            'mockk'                  : "io.mockk:mockk:${versions.mockk}",
            'mockkAndroid'           : "io.mockk:mockk-android:${versions.mockk}",
            'junitExt'               : "androidx.test.ext:junit:${versions.junitExt}",
            'json'                   : "org.json:json:${versions.json}",
            'google'                 : [
                    'gson': "com.google.code.gson:gson:${versions.gson}"
            ],
            'lottie'                 : "com.airbnb.android:lottie:${versions.lottie}",
            'youtubeExtractor'       : "com.github.maxrave-dev:kotlin-youtubeExtractor:${versions.youtubeExtractor}",
            'appcompatv7'            : "com.android.support:appcompat-v7:${versions.appcompatv7}",
            'playServicesAuth'       : "com.google.android.gms:play-services-auth:${versions.playServicesAuth}",
            'easypermissions'        : "pub.devrel:easypermissions:${versions.easypermissions}",
            'glide'                  : "com.github.bumptech.glide:glide:${versions.glide}",
            'firestore'              : "com.google.firebase:firebase-firestore-ktx:${versions.firestore}",
    ]

    repositories {
        mavenCentral()
        google()
        jcenter()
        gradlePluginPortal()
        flatDir {
            dirs 'libs'
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.4.10"
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:8.2.0'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"
        classpath 'com.google.gms:google-services:4.3.10'
    }
}

allprojects {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all Kotlin stdlib artifacts to use the same version.
                if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name.startsWith('kotlin-stdlib')) {
                    details.useVersion versions.kotlin
                }
            }
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(Test) {
        testLogging {
            events "skipped", "failed", "passed"
        }
    }


}
task clean(type: Delete) {
    delete rootProject.buildDir
}